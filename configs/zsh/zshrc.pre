#=====================================================================
# default configs which do not suggest to modify
# for 0/1 pairs, 0 -> disable; 1 -> enable

#---------------------------------------------------------------------
# default editor
export EDITOR=nvim

#---------------------------------------------------------------------
# rtx is an 'asdf'-like tool to install other binaries, but more user-friendly and fast
# rtx global config: ~/.tool-versions
# DO NOT suggest to modify this unless you are on an offline server
export USE_RTX=1

#=====================================================================
# customized exports, can be overrided in ~/.zshrc.private or ~/.zshrc.local
# if need to config different for specific directories, you can use tool 'direnv'

#---------------------------------------------------------------------
# proxy (use 'setproxy/unsetproxy' to toggle proxy)
export PROXY_IP="127.0.0.1:1080"
export PROXY_ENABLED=0
#---------------------------------------------------------------------
# disable security setting to avoid some downlink issues
export SKIP_SSL=0
#---------------------------------------------------------------------
# pueued
export PUEUED_AUTO_START=0
#---------------------------------------------------------------------
# terminal theme: light | dark
export TERMINAL_THEME='dark'
#---------------------------------------------------------------------
# nvim extensions
# auto format or not
export NVIM_FORMAT_ON_SAVE=false
# show diag on hover or not
export NVIM_SHOW_DIAG_ON_HOVER=false
export NVIM_SHOW_DIAG_ON_HOVER_SERVERITY_MIN="error"  # error/warn/info/hint
#---------------------------------------------------------------------
# For ChatGPT.nvim and 'sgpt' CLI command
# export OPENAI_API_KEY=""
#---------------------------------------------------------------------

#=====================================================================
# create directories & modify PATH

# Create ~/bin
_DIRCREATE ~/bin
# Create nvim persistent undo directory
_DIRCREATE ~/.cache/nvim/undo
# Create npm local directory for nvim lsp
# Reference: https://docs.npmjs.com/resolving-eacces-permissions-errors-when-installing-packages-globally#manually-change-npms-default-directory
_DIRCREATE ~/.npm-global/bin && export NPM_CONFIG_PREFIX=~/.npm-global

_PATHPREPEND /usr/lib64/jvm/java-17-openjdk/bin  # For ArchLinux
_PATHAPPEND ~/bin ~/.cargo/bin ~/.npm-global/bin ~/.go/bin ~/.local/bin ~/.local/share/nvim/mason/bin

#=====================================================================
# exports for specific command line tool

# cat
if command -v bat &> /dev/null; then
    alias cat="bat --paging=never"
fi

# cmake
if command -v cmake &> /dev/null; then
    export CMAKE_EXPORT_COMPILE_COMMANDS=1
fi

# cscope
if command -v cscope &> /dev/null; then
    # use same generate files as `cscope_maps.nvim`
    alias cscope="cscope -f ./cscope.out"
fi

# macchina
if command -v macchina &> /dev/null && [[ -f ~/.config/macchina/themes/Lithium.toml ]]; then
    alias macchina="macchina --theme Lithium"
fi

# nnn
if [[ -f ~/bin/nnn ]]; then
    alias nnn="~/bin/nnn"
fi
if command -v nnn &> /dev/null; then
    export NNN_FIFO="/tmp/nnn.fifo" # temporary buffer for the previews
    # nnn plugins reference: https://github.com/jarun/nnn/tree/master/plugins
    export NNN_PLUG='c:!code $nnn*;d:diffs;t:fzopen;p:preview-tui;z:autojump'
    nn()
    {
        if [ -z "$TMUX" ]; then
            nnn -e "$@"
        else
            nnn -e -P p "$@"
        fi
    }
    n() # to cd on quit
    {
        if [ -n $NNNLVL ] && [ "${NNNLVL:-0}" -ge 1 ]; then
            echo "nnn is already running"
            return
        fi
        export NNN_TMPFILE="${XDG_CONFIG_HOME:-$HOME/.config}/nnn/.lastd"
        if [ -z "$TMUX" ]; then
            nnn -e "$@"
        else
            nnn -e -P p "$@"
        fi
        if [ -f "$NNN_TMPFILE" ]; then
            . "$NNN_TMPFILE"
            rm -f "$NNN_TMPFILE" > /dev/null
        fi
    }
fi

# sg (ast-grep)
if command -v ast-grep &> /dev/null; then
    alias sg="ast-grep"
fi

#=====================================================================
# fuctions for command line use

# cdup : same as 'cd ..'
# cdup <N> (N=1~9): same as 'cd ../../..' (repeat N times)
# cdup <name>: find the nearest directory and cd into it (not support glob)
function cdup() {
    if [ $# -eq 0 ]; then
        cd ..
    elif [ $# -eq 1 ]; then
        if [[ $1 =~ '^[1-9]$' ]]; then
            local targetdir=""
            for i in {1..$1}; do
                targetdir+="../"
            done
            cd $targetdir
        elif [ $1 = '/' ]; then
            cd /
        else
            parentdir=${${PWD}%/*}/
            if [[ $parentdir == */$1/* ]]; then
                cd ${PWD%/$1/*}/$1
            fi
        fi
    fi
}
alias cu="cdup"

# cd to current/parent dirs which contains input filename
function cdup_to_exists() {
    if [ $# -eq 0 ]; then
        echo "fatal: please specify a pattern or directory, such as .root or .git"
    elif [ $# -eq 1 ]; then
        dir=${PWD}
        while [[ ! -e $dir/$1 ]] && [[ $dir != '/' ]]; do
            dir="$(dirname "$dir")"
        done
        if [[ ! -e $dir/$1 ]]; then
            echo "fatal: file $1 not found in current and parent dirs"
        else
            cd $dir
        fi
    fi
}
alias ce="cdup_to_exists"

# use fzf to scan 'wd' directories
if command -v _wdi &> /dev/null; then
    function wdi() {
        cd "$(_wdi "$*")"
    }
fi

# Theme settings
function set_theme() {
    unalias bat &> /dev/null
    unset NNN_FCOLORS
    case $1 in
        light)
            export TERMINAL_THEME="light"
            # bat: use `bat --list-thmes` to check available colors
            export BAT_THEME="OneHalfLight"
            export PROCS_THEME="light"
            # zsh-autosuggestions: https://github.com/zsh-users/zsh-autosuggestions#suggestion-highlight-style
            export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=252"  # default color: 8
            command -v r2 &> /dev/null && echo "eco default" > ~/.radare2rc
            ;;
        dark)
            export TERMINAL_THEME="dark"
            export BAT_THEME="OneHalfDark"
            export PROCS_THEME="dark"
            BLK="04" CHR="04" DIR="04" EXE="00" REG="00" HARDLINK="00" SYMLINK="06" MISSING="00" ORPHAN="01" FIFO="0F" SOCK="0F" OTHER="02"
            export NNN_FCOLORS="$BLK$CHR$DIR$EXE$REG$HARDLINK$SYMLINK$MISSING$ORPHAN$FIFO$SOCK$OTHER"
            export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=242"
            command -v r2 &> /dev/null && echo "eco onedark" > ~/.radare2rc
            ;;
        *)
            ;;
    esac
    alias bat="bat --theme=${BAT_THEME}"
    alias procs="procs --theme ${PROCS_THEME}"
    ln -sf ~/.dotfiles/configs/tmux/.tmux.conf.theme.${TERMINAL_THEME} ~/.tmux.conf.theme
}
alias setlight="set_theme light"
alias setdark="set_theme dark"

######################################################################
# ALIASES

# clear: do not delete scrollback buffer (requires: tput version >= 6.2)
if tput -x clear &> /dev/null; then
    alias clear='tput -x clear'
fi

# rg
alias rg='rg --smart-case'

# zoxide: a rust implementation for z / autojump
if rtx which zoxide &> /dev/null; then
    eval "$($(rtx which zoxide) init zsh)"
elif command -v zoxide &> /dev/null; then
    eval "$(zoxide init zsh)"
fi

# lsd -> l
if command -v lsd &> /dev/null; then
    if xset q &>/dev/null && ! grep -qEi "WSL" /proc/version; then
        alias lsd="lsd --group-directories-first --date='+%Y-%m-%d %H:%M' --hyperlink=always"
    else
        alias lsd="lsd --group-directories-first --date='+%Y-%m-%d %H:%M'"
    fi
    alias ls="lsd"
    alias l="lsd --git"
    alias la="lsd -Ah"
    alias ll="lsd -lh --git"
    alias lla="lsd -lAh --git"
    alias l1="lsd -1"
    alias l2="lsd --tree --depth=2"
    alias l3="lsd --tree --depth=3"
    alias l4="lsd --tree --depth=4"
    alias l5="lsd --tree --depth=5"
else
    alias ls='ls --color=auto'
    alias l='ls --color=auto'
    alias la="ls -ah"
    alias ll="ls -lh"
    alias lla="ls -lah"
fi

# nvim -> v
if command -v nvim &> /dev/null || rtx which nvim &> /dev/null; then
    alias v='nvim'
fi

# git -> g
if command -v git &> /dev/null; then
    alias g='git'
fi

# tmux -> t
if command -v tmux &> /dev/null || rtx which tmux &> /dev/null; then
    function t() {
        # set default $session to $1 or 0 (if $1 not exist)
        local session="${1:-0}"
        if command -v tmuxinator &> /dev/null && [ -f ~/.tmuxinator/${session}.yml ]; then
            tmuxinator $session
        else
            if tmux has -t $session &> /dev/null; then
                if [ -z "$TMUX" ]; then
                    tmux attach -t $session
                else
                    tmux switch -t $session
                fi
            else
                if [ -z "$TMUX" ]; then
                    tmux new -s $session
                else
                    tmux new -s $session -d && tmux switch -t $session
                fi
            fi

        fi
    }
    alias tl='tmux list-sessions'
    # kill all sessions except session 0
    alias tk='tmux kill-session -at 0 &> /dev/null || tmux kill-server'
    # kill session 0
    alias tk0='tmux kill-session -t 0 &> /dev/null'
    # kill all sessions
    alias tka='tmux kill-server'
fi

# Use rg (ripgrep) as fzf's **<TAB> / CTRL-T backend
if command -v rg &> /dev/null; then
    export FZF_DEFAULT_COMMAND="rg --files --no-ignore --hidden --follow --glob '!{.git,.repo,.cache,.vscode,.build,.clangd,build,bazel-*}/*' --glob '!{.cs_files,.project,.root,.workspace,compile_commands.json,cscope.*}' 2> /dev/null"
    export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
fi

# fzf
# Reference: https://man.archlinux.org/man/community/fzf/fzf.1.en
# Use same keybindings as `fzf-tab` tool
export FZF_DEFAULT_OPTS="--bind left:preview-page-up,right:preview-page-down,ctrl-/:toggle-preview,ctrl-a:toggle-all,ctrl-s:toggle-out,tab:up,shift-tab:down"
export FZF_CTRL_T_OPTS="--bind left:preview-page-up,right:preview-page-down,ctrl-/:toggle-preview,ctrl-a:toggle-all,ctrl-s:toggle-out,tab:down,shift-tab:up"
export FZF_CTRL_R_OPTS="--bind left:preview-page-up,right:preview-page-down,ctrl-/:toggle-preview,ctrl-a:toggle-all,ctrl-s:toggle-out,tab:up,shift-tab:down"

######################################################################
